chain: socket'
{{
    : ?CR  
	over 1+ @ 'n = [ drop over 10 swap ! 1+ 0 ] [ -1 ] if ;
	    
    : ?NL  
	over 1+ @ 'r = [ drop over 13 swap ! 1+ 0 ] [ -1 ] if ;

    : ?ESCAPE 
	dup '\ = [ ?CR [ ?NL ] [ 0 ]  if ] [ -1 ]  if ;
	    
    : COPYNULL
	dup 1+ @ 0 = [ over 1+ 0 swap ! ] ifTrue ;
	    
    : COPYVAL
	[ swap push over ! pop ] ifTrue ;
	    
    : _NORMALIZE ( string -- string endread endwrite ) 
	2dup repeat dup @ 0; 
	?ESCAPE COPYVAL COPYNULL 
	1+ swap 1+ swap again ;
	    
---reveal---    
	
    : NORMALIZE 
	_NORMALIZE 2drop ;
	    
    : PRINTCHARS ( string -- )
	swap [ dup @ putc 1+ ] times drop ;
	
    : connect ( addr port -- sock )         
	#1 #15 out #0 #0 out wait ;
    : close   ( sock -- status )             
	#2 #15 out #0 #0 out wait ;
    : read    ( sock count buffer -- count )   
	#3 #15 out #0 #0 out wait ;
    : write   ( sock length buffer -- count ) 
	#4 #15 out #0 #0 out wait ;

    : copyargs ( v1 v2 v3 -- v1 v2 v3 v1 v2 v3 )
	[ 2over ] dip dup [ 2rot ] dip ;
}}
;chain

( Create read buffer )
create buffer 1024 allot 

( Create socket )
variable sock

: testTCPClient ( -- closeStatus )

    ( Connect assign socket )
    "www.osnews.com" 80 ^socket'connect sock ! 

    ( Request Page )
    sock @ "GET / HTTP/1.1\r\nHost: www.osnews.com\r\n\r\n" 
    ^socket'NORMALIZE withLength swap ^socket'write drop

    ( Print results )
    sock @ 1024 buffer
    [ ^socket'copyargs ^socket'read dup buffer ^socket'PRINTCHARS 0 > ] while
    
    ( drop result of copyargs left on the stack )
    2drop drop

    ( Close Socket )
    sock @ ^socket'close
;

testTCPClient

