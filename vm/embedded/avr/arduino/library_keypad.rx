chain: keypad' {{

  variables| k-cols k-rows k-ddr k-port k-pin |

  : init-cols ( n-n )
    @k-cols [ dup [ over = ] dip @k-port ^arduino'io-bit! ] iter ;
  : get-row ( n-n )
    @k-cols + @k-pin ^arduino'io-bit@ ;
  : check-rows ( n-n )
    dup 0 = if; dup 1- get-row 0 = [ 1- check-rows ] ifTrue ;
  : check-all ( n-n )
    dup 0 = if; 1- init-cols @k-rows check-rows dup
    0 > [ swap @k-rows * + ] [ drop check-all ] if ;

---reveal---

  ( It works only with maximal 4 columns and maximal 4 rows )
  ( of buttons.                                             )
  ( Arguments: cols,rows,ddr,port,pin                       )
  : init-keypad ( nnnnn- ) 
    !k-pin !k-port !k-ddr !k-rows !k-cols
    @k-cols [ 1 swap @k-ddr ^arduino'io-bit! ] iter
    @k-rows [ 0 swap @k-cols + @k-ddr ^arduino'io-bit! ] iter ;

  ( Returns the currently pressed key )
  : pressed-key ( -n )
    @k-cols check-all ;

  ( Wait for a key press )
  : wait-key ( -n )
    [ pressed-key dup 0 = [ drop -1 50 ] [ 0 200 ] if ^arduino'sleep ] while ;
    
}} ;chain
